	.data
str1:	.asciiz "Arquitetura de "
str2:	.space 50
str3:	.asciiz "\n"
str4:	.asciiz "Computadores I"
	.eqv print_string,4
	.text
	.globl main

main:
	addiu $sp,$sp,-4
	sw $ra,0($sp)

	la $a0,str2		# $a0 = str2
	la $a1,str1		# $a1 = str1
	jal strcpy		# strcpy(str2,str1)

	la $a0,str2
	li $v0,print_string
	syscall			# print_string(str2)

	la $a0,str3
	li $v0,print_string
	syscall			# print_string('\n')

	la $a0,str2
	la $a1,str4
	jal strcat		# strcat(str2,"Computadores I")

	move $a0,$v0
	li $v0,print_string
	syscall			#print_string(strcat(str2,"Computadores I"))
	
	li $v0,0		#return 0
	
	lw $ra,0($sp)
	addiu $sp,$sp,4

	jr $ra

#######################################################################################

strcpy:
	li $t2,0	#i=0
	
do:
	addu $t1,$a1,$t2	#&src[i]
	addu $t0,$a0,$t2	#&dst[i]

	lb $t3,0($t1)		#src[i]
	sb $t3,0($t0)		#dst[i]=src[i]

	addi $t2,$t2,1		#i++
while4:
	bne $t3,'\0',do		#while[src[i++] != '\0'

	move $v0,$a0		#return dst

	jr $ra

#######################################################################################

strcat:
	addiu $sp,$sp,-12
	sw $ra,0($sp)
	sw $s0,4($sp)
	sw $s1,8($sp)

	move $s0,$a0		#$s0=p
	move $s1,$a1		#$s1=src

while:	lb $t0,0($s0)		#*p

	beq $t0,'\0',endwhile	#*p!= '\0'

	addiu $s0,$s0,1		#p++

	j while
endwhile:

	move $a0,$s0		
	move $a1,$s1
	jal strcpy		#strcpy(p,src) #return dst($v0=dst) quando se faz o strcpy

	lw $ra,0($sp)
	lw $s0,4($sp)
	lw $s1,8($sp)
	addiu $sp,$sp,12

	jr $ra